def PROGUARD = false

def APP = [
  'VERSION_CODE'       : 1,
  'VERSION_NAME'       : '1.0.0',
  'APPLICATION_ID'     : 'com.kwf2030.android',
  'MIN_SDK_VERSION'    : 16,
  'TARGET_SDK_VERSION' : 23,
  'COMPILE_SDK_VERSION': 23,
  'BUILD_TOOLS_VERSION': '24.0.0'
]

def LIB = [
  'SUPPORT'      : '24.0.0',
  'BOTTOMBAR'    : '1.3.9',
  'DAGGER'       : '2.5',
  'GSON'         : '2.7',
  'JODATIME'     : '2.9.4',
  'OKHTTP'       : '3.3.1',
  'RETROFIT'     : '2.1.0',
  'REQUERY'      : '1.0.0-beta22',
  'RXANDROID'    : '1.2.1',
  'RXCACHE'      : '1.4.7',
  'RXJAVA'       : '1.1.6',
  'RXPERMISSION' : '0.7.0',

  'DEXMAKER'    : '1.2',
  'ESPRESSO'    : '2.2.2',
  'JUNIT_RUNNER': '0.5',
  'JUNIT4_RULES': '0.5',
  'JUNIT'       : '4.12',
  'LEAKCANARY'  : '1.4-beta2',
  'MOCKITO'     : '2.0.54-beta'
]

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:2+'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1+'
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
  maven { url "https://jitpack.io" }
  //maven { url 'http://192.168.1.2:8081/nexus/content/groups/public/' }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
  distributionUrl = "http://192.168.1.2:8082/files/gradle-${gradleVersion}-bin.zip"
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

configurations {
  compile {
    exclude module: 'guava'
    testCompile.extendsFrom compile
    androidTestCompile.extendsFrom testCompile
  }
}

dependencies {
  apt "com.google.dagger:dagger-compiler:$LIB.DAGGER"
  apt "io.requery:requery-processor:$LIB.REQUERY"

  compile "com.android.support:appcompat-v7:$LIB.SUPPORT"
  compile "com.android.support:design:$LIB.SUPPORT"

  compile "com.roughike:bottom-bar:$LIB.BOTTOMBAR"

  compile "com.google.dagger:dagger:$LIB.DAGGER"

  compile "com.google.code.gson:gson:$LIB.GSON"

  compile "joda-time:joda-time:$LIB.JODATIME"

  compile "com.squareup.okhttp3:okhttp:$LIB.OKHTTP"
  compile "com.squareup.okhttp3:logging-interceptor:$LIB.OKHTTP"
  compile "com.squareup.okhttp3:okhttp-android-support:$LIB.OKHTTP"

  compile "com.squareup.retrofit2:retrofit:$LIB.RETROFIT"
  compile "com.squareup.retrofit2:adapter-rxjava:$LIB.RETROFIT"
  compile "com.squareup.retrofit2:converter-gson:$LIB.RETROFIT"

  compile "io.requery:requery:$LIB.REQUERY"
  compile "io.requery:requery-android:$LIB.REQUERY"

  compile "io.reactivex:rxandroid:$LIB.RXANDROID"
  compile "com.github.VictorAlbertos:RxCache:$LIB.RXCACHE"
  compile "io.reactivex:rxjava:$LIB.RXJAVA"

  debugCompile "com.squareup.leakcanary:leakcanary-android:$LIB.LEAKCANARY"

  releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LIB.LEAKCANARY"

  testCompile "junit:junit:$LIB.JUNIT"
  testCompile "org.mockito:mockito-core:$LIB.MOCKITO"
  testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LIB.LEAKCANARY"
  testCompile "com.squareup.okhttp3:mockwebserver:$LIB.OKHTTP"

  androidTestCompile "com.google.dexmaker:dexmaker:$LIB.DEXMAKER"
  androidTestCompile "com.google.dexmaker:dexmaker-mockito:$LIB.DEXMAKER"
  androidTestCompile "com.android.support.test.espresso:espresso-core:$LIB.ESPRESSO"
  androidTestCompile "com.android.support.test.espresso:espresso-contrib:$LIB.ESPRESSO"
  androidTestCompile "com.android.support.test.espresso:espresso-web:$LIB.ESPRESSO"
  androidTestCompile "com.android.support.test.espresso:espresso-idling-resource:$LIB.ESPRESSO"
  androidTestCompile "com.android.support.test:runner:$LIB.JUNIT_RUNNER"
  androidTestCompile "com.android.support.test:rules:$LIB.JUNIT4_RULES"
}

android {
  compileSdkVersion APP.COMPILE_SDK_VERSION
  buildToolsVersion APP.BUILD_TOOLS_VERSION

  defaultConfig {
    applicationId APP.APPLICATION_ID
    minSdkVersion APP.MIN_SDK_VERSION
    targetSdkVersion APP.TARGET_SDK_VERSION
    versionCode APP.VERSION_CODE
    versionName APP.VERSION_NAME

    multiDexEnabled true

    /*jackOptions {
      enabled true
    }*/

    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
  }

  buildTypes {
    debug {
      debuggable true
      minifyEnabled false
      //buildConfigField "boolean", "CONST", "true"
    }

    release {
      debuggable false
      minifyEnabled PROGUARD
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      //signingConfig signingConfigs.release
      zipAlignEnabled true
      shrinkResources PROGUARD
    }
  }

  packagingOptions {
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/rxjava.properties'
  }

  compileOptions {
    encoding 'UTF-8'
    sourceCompatibility 1.7
    targetCompatibility 1.7
  }

  lintOptions {
    abortOnError false
  }

  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
  }
}
